plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    modImplementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_api_version}"
    compileOnly "maven.modrinth:iris:${iris_version}+${minecraft_version}-neoforge"
    localRuntime "maven.modrinth:sodium:mc${minecraft_version}-${sodium_version}-neoforge"
    localRuntime "maven.modrinth:iris:${iris_version}+${minecraft_version}-neoforge"

    implementation "org.anarres:jcpp:1.4.14"
    implementation "org.antlr:antlr4-runtime:4.13.1"
    implementation "io.github.douira:glsl-transformer:2.0.1"

    implementation 'ar.com.hjg:pngj:2.1.0'
    include 'ar.com.hjg:pngj:2.1.0'

    //modLocalRuntime "maven.modrinth:renderscale:1.3.6"
    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
